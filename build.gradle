import org.springframework.boot.gradle.plugin.SpringBootPlugin

import java.time.Instant
import java.time.LocalDate

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'groovy'
    id 'org.springframework.boot' version '2.3.0.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'org.owasp.dependencycheck' version '5.3.2.1'
    id 'com.github.ben-manes.versions' version '0.28.0'
    id 'org.jetbrains.kotlin.plugin.spring' version '1.3.72'
    id 'org.jetbrains.kotlin.plugin.jpa' version '1.3.72'
    id 'se.patrikerdes.use-latest-versions' version '0.2.13'
}

apply plugin: "kotlin-allopen"
apply plugin: "kotlin-jpa"

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    agentDeps
    agentDeps.transitive = false
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = '11'
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = '11'
    }
}


project.sourceCompatibility = 11
project.targetCompatibility = 11

dependencyCheck {
    failBuildOnCVSS = 5
    suppressionFiles = ['dependency-check-suppress-spring.xml']
    format = 'ALL'
    analyzers {
        assemblyEnabled = false
    }
}

group = 'uk.gov.justice.digital.hmpps'

def today = Instant.now()
def todaysDate = LocalDate.now().format('yyyy-MM-dd')
version = System.getenv('CI') ? "${todaysDate}.${System.getenv('CIRCLE_BUILD_NUM')}" : todaysDate

jar {
    baseName = rootProject.name
    manifest {
        attributes 'Implementation-Title': rootProject.name, 'Implementation-Version': version
    }
}

springBoot {
    buildInfo {
        properties {
            artifact = rootProject.name
            version = version
            group = group
            name = rootProject.name
            time = today
            additional = [
                    by                   : System.properties['user.name'],
                    operatingSystem      : "${System.properties['os.name']} (${System.properties['os.version']})",
                    continuousIntegration: System.getenv('CI') ? true : false,
                    machine              : InetAddress.localHost.hostName
            ]
        }
    }
}

configurations {
    implementation.exclude module: 'tomcat-jdbc'
}

dependencyManagement {
    imports {
        mavenBom(SpringBootPlugin.BOM_COORDINATES)
    }
}

ext ['spring-security.version'] = '5.3.2.RELEASE' // Pinned to resolve CVE-2018-1258

dependencies {
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok:1.18.12'

    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok:1.18.12'

    runtime 'com.h2database:h2:1.4.200'
    runtime 'org.flywaydb:flyway-core:6.4.2'
    runtime 'org.postgresql:postgresql:42.2.12'

    implementation 'org.hibernate:hibernate-java8'

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-cache'

    implementation 'org.springframework.boot:spring-boot-starter-security'

    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.security:spring-security-oauth2-jose'

    implementation 'org.springframework:spring-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-reactor-netty'

    implementation 'net.logstash.logback:logstash-logback-encoder:6.3'
    implementation 'com.microsoft.azure:applicationinsights-spring-boot-starter:2.6.1-BETA'
    implementation 'com.microsoft.azure:applicationinsights-logging-logback:2.6.1-BETA'
    agentDeps 'com.microsoft.azure:applicationinsights-agent:2.6.1-BETA'

    implementation 'com.github.timpeeters:spring-boot-graceful-shutdown:2.2.2'

    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'javax.xml.bind:jaxb-api:2.4.0-b180830.0359'
    implementation 'com.sun.xml.bind:jaxb-impl:3.0.0-M3'
    implementation 'com.sun.xml.bind:jaxb-core:3.0.0-M3'
    implementation 'javax.activation:activation:1.1.1'
    implementation 'javax.transaction:javax.transaction-api:1.3'

    implementation 'io.springfox:springfox-swagger2:2.9.2'
    implementation 'io.springfox:springfox-swagger-ui:2.9.2'

    implementation 'net.sf.ehcache:ehcache:2.10.6'
    implementation 'org.apache.commons:commons-lang3:3.10'
    implementation 'org.apache.commons:commons-text:1.8'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.11.0'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.11.0'
    implementation 'com.pauldijou:jwt-core_2.11:4.3.0'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.google.guava:guava:29.0-jre'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'

    implementation 'org.springframework:spring-jms'
    implementation 'com.amazonaws:amazon-sqs-java-messaging-lib:1.0.8'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.github.http-builder-ng:http-builder-ng-apache:1.0.4'
    testImplementation 'net.javacrumbs.json-unit:json-unit-assertj:2.17.0'
    testImplementation 'com.github.tomakehurst:wiremock-standalone:2.26.3'
    testImplementation 'com.nhaarman:mockito-kotlin-kt1.1:1.6.0'
    testImplementation 'org.testcontainers:localstack:1.13.0'

    testCompileOnly 'org.projectlombok:lombok:1.18.12'
    testImplementation 'io.jsonwebtoken:jjwt:0.9.1'

    compile configurations.agentDeps
}


test {
    useJUnitPlatform()
}

task copyAgent(type: Copy) {
    from configurations.agentDeps
    into "$buildDir/libs"
}

assemble.dependsOn copyAgent
